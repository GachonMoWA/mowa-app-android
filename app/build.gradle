plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
    id 'com.google.gms.google-services'
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

// Dialogflow
def dialogflowCredentialDir = properties.getProperty('dialogflow.credential.dir')

// Base url
def serverBaseUrl = properties.getProperty('server.base.url')
def openWeatherBaseUrl = properties.getProperty('open.weather.base.url')
def welfareCenterBaseUrl = properties.getProperty('welfare.center.base.url')

// API key
def kakaoMapApiKey = properties.getProperty('kakao.map.api.key')
def openWeatherApiKey = properties.getProperty('open.weather.api.key')
def welfareCenterApiKey = properties.getProperty('welfare.center.api.key')

android {
    compileSdk 32
    defaultConfig {
        applicationId "com.gachon.mowa"
        minSdk 23
        targetSdk 32
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        // Dialogflow
        buildConfigField "String", "DIALOGFLOW_CREDENTIAL_DIR", "\"$dialogflowCredentialDir\""

        // Base url
        buildConfigField "String", "SERVER_BASE_URL", "\"$serverBaseUrl\""
        buildConfigField "String", "OPEN_WEATHER_BASE_URL", "\"$openWeatherBaseUrl\""
        buildConfigField "String", "WELFARE_CENTER_BASE_URL", "\"$welfareCenterBaseUrl\""

        // API key
        buildConfigField "String", "KAKAO_MAP_API_KEY", "\"$kakaoMapApiKey\""
        buildConfigField "String", "OPEN_WEATHER_API_KEY", "\"$openWeatherApiKey\""
        buildConfigField "String", "WELFARE_CENTER_API_KEY", "\"$welfareCenterApiKey\""

        manifestPlaceholders = [KAKAO_MAP_API_KEY: kakaoMapApiKey]
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    viewBinding {
        enabled true
    }
    dataBinding {
        enabled true
    }
    packagingOptions {
        resources {
            excludes += ['META-INF/DEPENDENCIES', 'META-INF/LICENSE', 'META-INF/LICENSE.txt', 'META-INF/license.txt', 'META-INF/NOTICE', 'META-INF/NOTICE.txt', 'META-INF/INDEX.LIST', 'META-INF/notice.txt', 'META-INF/ASL2.0']
        }
    }

    namespace 'com.gachon.mowa'
}

dependencies {

    implementation 'androidx.core:core-ktx:1.8.0'
    implementation 'androidx.appcompat:appcompat:1.4.2'
    implementation 'com.google.android.material:material:1.6.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'com.google.android.gms:play-services-maps:18.1.0'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.12.1'

    // navigation
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"

    // RecyclerView
    implementation "androidx.recyclerview:recyclerview:$recyclerview_version"

    // DrawerLayout
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'

    // Firebase
    implementation platform('com.google.firebase:firebase-bom:30.2.0')

    // Firebase login
    implementation 'com.google.firebase:firebase-auth'
    implementation 'com.google.android.gms:play-services-auth:20.4.0'

    // Retrofit
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"

    // TabLayout
    implementation 'com.google.android.material:material:1.0.0-rc01'

    // OkHttp
    implementation "com.squareup.okhttp3:okhttp:$okHttp_version"
    implementation "com.squareup.okhttp3:logging-interceptor:$okHttp_version"

    // Room
    implementation "androidx.room:room-runtime:$roomdb_version"
    kapt "androidx.room:room-compiler:$roomdb_version"

    // Gson
    implementation 'com.google.code.gson:gson:2.9.0'

    // indicator
    implementation 'com.tbuonomo:dotsindicator:4.2'

    // Kakao Map API
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation files('libs/libDaumMapAndroid.jar')

    // Coroutine
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.1'

    // Dialogflow
    implementation 'com.google.cloud:google-cloud-dialogflow:2.1.0'
    implementation 'io.grpc:grpc-okhttp:1.30.0'
    implementation 'com.google.protobuf:protobuf-gradle-plugin:0.9.1'

    /* dialogflow-android-client */

    implementation 'ai.api:sdk:2.0.7@aar'
    // api.ai SDK dependencies
    implementation 'androidx.appcompat:appcompat:1.0.0'

    // for a class that implements the AIListener interface
    implementation 'ai.api:libai:1.6.12'
}